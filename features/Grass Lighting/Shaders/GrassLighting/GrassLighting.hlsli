#include "Common/BRDF.hlsli"
#include "Common/Color.hlsli"

namespace GrassLighting
{
	float GetRainWetness()
	{
#if defined(WETNESS_EFFECTS)
		return saturate(SharedData::wetnessEffectsSettings.Wetness * SharedData::wetnessEffectsSettings.MaxRainWetness);
#else
		return 0.0;
#endif
	}

	float3 GetWetnessAlbedo(float3 albedo, float wetAmount)
	{
		float wetnessDarkeningAmount = wetAmount * wetAmount;
		return lerp(albedo, pow(abs(albedo), 1.0 + wetnessDarkeningAmount), 0.5);
	}

	float3 SafeNormalize(float3 v, float3 fallback)
	{
		float lengthSq = dot(v, v);
		return lengthSq > 1e-12 ? v * rsqrt(lengthSq) : fallback;
	}

	float GetSoftLightMultiplier(float angle, float strength)
	{
		float softLightParam = saturate((strength + angle) / (1.0 + strength));
		float wrapped = (softLightParam * softLightParam) * (3.0 - 2.0 * softLightParam);
		float clampedAngle = saturate(angle);
		float direct = (clampedAngle * clampedAngle) * (3.0 - 2.0 * clampedAngle);
		return saturate(wrapped - direct);
	}

	float3 GetTransmissionTint(float3 albedo, float saturation)
	{
		albedo = max(albedo, 0.0);

		// Leaf reflectance and transmittance have similar spectral shapes, so use
		// the surface albedo directly and only adjust its transmission chroma.
		float3 transmissionYCoCg = Color::RGBToYCoCg(albedo);

		// Rotate green-dominant colors by up to 10 degrees toward yellow in the
		// Co/Cg plane. Non-green or already yellowed grass keeps its original hue.
		float maxChannel = max(max(albedo.r, albedo.g), albedo.b);
		float greenDominance = saturate((albedo.g - max(albedo.r, albedo.b)) / max(maxChannel, 1e-4));
		float hueSin = 0.17364818 * greenDominance;
		float hueCos = lerp(1.0, 0.98480775, greenDominance);
		float2 chroma = transmissionYCoCg.yz;
		transmissionYCoCg.y = hueCos * chroma.x + hueSin * chroma.y;
		transmissionYCoCg.z = hueCos * chroma.y - hueSin * chroma.x;

		transmissionYCoCg.yz *= saturation;
		return max(Color::YCoCgToRGB(transmissionYCoCg), 0.0);
	}

	float GetTransmissionFactor(float NdotL, float VdotL, float amount)
	{
		float backLight = saturate(-NdotL);
		backLight = backLight * backLight * (3.0 - 2.0 * backLight);

		float forwardScatter = saturate(-VdotL);
		forwardScatter *= forwardScatter;

		return 0.5 * amount * backLight * lerp(0.25, 1.0, forwardScatter);
	}

	float3 GetLightSpecularInput(float3 L, float3 V, float3 N, float3 lightColor, float roughness, float3 F0)
	{
		float3 H = normalize(V + L);
#if defined(VANILLA_FRESNEL)
		if (SharedData::vanillaFresnelSettings.Enable && SharedData::vanillaFresnelSettings.EnableGGXOnGrass) {
			float NdotL = saturate(dot(N, L));
			float NdotV = saturate(dot(N, V));
			float NdotH = saturate(dot(N, H));
			float VdotH = saturate(dot(V, H));

			float D = BRDF::D_GGX(roughness, NdotH);
			float G = BRDF::Vis_SmithJointApprox(roughness, NdotL, NdotV);
			float3 F = BRDF::F_Schlick(F0, VdotH);
			float3 specular = D * G * F;
			return specular * lightColor * NdotL * Color::PBRLightingCompensation;
		}
#endif
		float shininess = (1.0 - roughness) * 100.f;
		float HdotN = saturate(dot(H, N));
		float lightColorMultiplier = exp2(shininess * log2(HdotN)) * saturate(dot(N, L));
		return lightColor * lightColorMultiplier.xxx;
	}

	float3 TransformNormal(float3 normal)
	{
		return normal * 2 + -1.0.xxx;
	}

	// http://www.thetenthplanet.de/archives/1180
	float3x3 CalculateTBN(float3 N, float3 p, float2 uv)
	{
		// get edge vectors of the pixel triangle
		float3 dp1 = ddx_coarse(p);
		float3 dp2 = ddy_coarse(p);
		float2 duv1 = ddx_coarse(uv);
		float2 duv2 = ddy_coarse(uv);

		// solve the linear system
		float3 dp2perp = cross(dp2, N);
		float3 dp1perp = cross(N, dp1);
		float3 T = dp2perp * duv1.x + dp1perp * duv2.x;
		float3 B = dp2perp * duv1.y + dp1perp * duv2.y;

		// construct a scale-invariant frame
		float maxLengthSq = max(dot(T, T), dot(B, B));
		float invmax = maxLengthSq > 1e-12 ? rsqrt(maxLengthSq) : 0.0;
		return float3x3(T * invmax, B * invmax, N);
	}

	float3 ApplyComplexNormal(float3 geometricNormal, float3 normalSample, float3x3 tbn, float strength)
	{
		float3 decoded = TransformNormal(normalSample);
		float decodedLength = length(decoded);

		float validity = saturate((0.5 - abs(decodedLength - 1.0)) * 4.0);
		if (validity <= 0.0)
			return geometricNormal;

		decoded /= decodedLength;
		decoded.xy *= strength;

		float3 worldNormal = lerp(geometricNormal, mul(decoded, tbn), validity);
		return SafeNormalize(worldNormal, geometricNormal);
	}
}
